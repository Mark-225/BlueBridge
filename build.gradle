plugins{
    id "java"
    id "idea"
    id 'com.gradleup.shadow' version '8.3.0'
}

subprojects {

    apply plugin: 'java'

    version '2.1'

    //Contributors are added to this list in the order of their first contribution. Separate using a comma and space.
    def authors = 'Mark_225, Aurelien30000'

    sourceCompatibility = 21
    targetCompatibility = 21

    group 'de.mark225'

    repositories {
        mavenCentral()
        maven { url 'https://repo.bluecolored.de/releases' }
        maven { url "https://jitpack.io" }
        maven { url "https://repo.papermc.io/repository/maven-public/" }
    }

    configurations {
        provided
        implementation.extendsFrom provided
    }

    dependencies {
        provided "org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT"
        provided 'com.flowpowered:flow-math:1.0.3'
    }

    tasks.withType(Jar) {
        destinationDirectory = file("$rootDir/build/target")
    }

    processResources {
        filesMatching('plugin.yml') {
            expand([gradleVersion: version, contributors: authors])
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}