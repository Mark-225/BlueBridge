plugins{
    id "java"
    id "idea"
}

subprojects {

    apply plugin: 'java'

    version '2.1'

    //Add maintainers here (separated by a comma and space). This will be added to the plugin.yml.
    def authors = 'Mark_225'

    sourceCompatibility = 17
    targetCompatibility = 17

    group 'de.mark225'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    }

    configurations {
        provided
        implementation.extendsFrom provided
    }

    dependencies {
        provided "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
        provided 'com.flowpowered:flow-math:1.0.3'
    }

    tasks.withType(Jar) {
        destinationDirectory = file("$rootDir/build/target")
    }

    processResources {
        filesMatching('plugin.yml') {
            expand([gradleVersion: version, contributors: authors])
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}