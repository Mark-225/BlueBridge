plugins{
    id "java"
    id "idea"
}

subprojects {

    apply plugin: 'java'

    version '2.1'

    //Contributors are added to this list in the order of their first contribution. Separate using a comma and space.
    def authors = 'Mark_225, Aurelien30000'

    sourceCompatibility = 11
    targetCompatibility = 11

    group 'de.mark225'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    }

    configurations {
        provided
        implementation.extendsFrom provided
    }

    dependencies {
        provided "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"
        provided 'com.flowpowered:flow-math:1.0.3'
    }

    tasks.withType(Jar) {
        destinationDirectory = file("$rootDir/build/target")
    }

    processResources {
        filesMatching('plugin.yml') {
            expand([gradleVersion: version, contributors: authors])
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}