buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:8.3.0'
    }

}

apply plugin: 'com.gradleup.shadow'


dependencies {
    compileOnly 'de.bluecolored.bluemap:BlueMapAPI:2.7.2'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-text
    implementation 'org.apache.commons:commons-text:1.9'
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
}

tasks.build.dependsOn tasks.shadowJar

jar {
    archiveClassifier = 'NoDependencies'
}

shadowJar {
    zip64 = true
    mergeServiceFiles()
    relocate 'org.apache.commons.text', 'de.mark225.shadow.org.apache.commons.text'
    relocate 'org.apache.commons.lang3', 'de.mark225.shadow.org.apache.commons.lang3'
    archiveClassifier = ''
    def exclude_modules = project
            .configurations
            .provided
            .resolvedConfiguration
            .getLenientConfiguration()
            .getAllModuleDependencies()
            .collect {
                it.name
            }
    dependencies {
        exclude(dependency {
            exclude_modules.contains(it.name)
        })
    }
}

